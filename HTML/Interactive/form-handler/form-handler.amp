<meta name="ROBOTS" content="NOINDEX, NOFOLLOW">
%%[
    set @origin = HTTPRequestHeader("Referer")
    set @domain = "exacttarget"
    set @pattern = concat("^(https:\/\/(.*\.)?((",@domain,")\.com))($|\/)")
    set @match = RegExMatch(@origin, @pattern, 1)
]%%
<script runat="server">
    Platform.Load("core","1");
    var MATCH = Variable.GetValue("@match");
    if (!MATCH) { MATCH = null }
    HTTPHeader.SetValue("Access-Control-Allow-Methods","POST");
    HTTPHeader.SetValue("Access-Control-Allow-Origin",MATCH);
    Platform.Response.SetResponseHeader("Strict-Transport-Security","max-age=200"); 
    Platform.Response.SetResponseHeader("X-XSS-Protection","1; mode=block"); 
    Platform.Response.SetResponseHeader("X-Frame-Options","Deny"); 
    Platform.Response.SetResponseHeader("X-Content-Type-Options","nosniff"); 
    Platform.Response.SetResponseHeader("Referrer-Policy","strict-origin-when-cross-origin"); 
    Platform.Response.SetResponseHeader("Content-Security-Policy","default-src 'self'");
</script>
%%[
if length(@match) > 0 then  

    var @name 
    var @age 
    var @formCompletionStatus 
    var @errors 

    set @name = requestParameter("name")
    if empty(@name) then
        set @errors = concat(@errors, 'name')
    endif

    set @age = requestParameter("age")
    if empty(@age) then
        set @errors = concat(@errors, 'age')
    endif

    set @formCompletionStatus = iif(length(@errors) > 0, 'errors', 'complete')

    redirect(concat(cloudPagesUrl(9087,'formCompletionStatus',@formCompletionStatus,'name',@name,'age',@age,'errors',@errors)))

endif
]%%